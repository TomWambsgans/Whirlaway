WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

// Program structure
program = { SOI ~ constant_declaration* ~ function+ ~ EOI }

// Constants
constant_declaration = { "const" ~ identifier ~ "=" ~ number ~ ";" }

// Functions
function = { "fn" ~ identifier ~ "(" ~ parameter_list? ~ ")" ~ return_count? ~ "{" ~ statement* ~ "}" }
parameter_list = { identifier ~ ("," ~ identifier)* }
return_count = { "->" ~ number }

// Statements
statement = {
    single_assignment |
    array_access |
    array_assign |
    if_statement |
    for_statement |
    return_statement |
    function_call |
    assert_eq_statement |
    assert_not_eq_statement
}

return_statement = { "return" ~ (tuple_expression)? ~ ";" }
tuple_expression = { expression ~ ("," ~ expression)* }

single_assignment = { identifier ~ "=" ~ expression ~ ";" }

array_access = { identifier ~ "=" ~ identifier ~ "[" ~ expression ~ "]" ~ ";" }
array_assign = { identifier ~ "[" ~ expression ~ "]" ~ "=" ~ expression ~ ";" }

if_statement = { "if" ~ condition ~ "{" ~ statement* ~ "}" ~ else_clause? }

condition = {condition_eq | condition_diff}
condition_eq = { expression ~ "==" ~ expression }
condition_diff = { expression ~ "!=" ~ expression }

else_clause = { "else" ~ "{" ~ statement* ~ "}" }

for_statement = { "for" ~ identifier ~ "in" ~ expression ~ ".." ~ expression ~ "{" ~ statement* ~ "}" }

function_call = { function_res? ~ identifier ~ "(" ~ argument_list? ~ ")" ~ ";" }
function_res = { var_list ~ "=" }
var_list = { identifier ~ ("," ~ identifier)* }
argument_list = { expression ~ ("," ~ expression)* }

assert_eq_statement = { "assert" ~ expression ~ "==" ~ expression ~ ";" }
assert_not_eq_statement = { "assert" ~ expression ~ "!=" ~ expression ~ ";" }

// Expressions
expression = { add_expr }
add_expr = { sub_expr ~ ("+" ~ sub_expr)* }
sub_expr = { mul_expr ~ ("-" ~ mul_expr)* }
mul_expr = { div_expr ~ ("*" ~ div_expr)* }
div_expr = { primary ~ ("/" ~ primary)* }
primary = { "(" ~ expression ~ ")" | var_or_constant }

// Basic elements
var_or_constant = { constant_value | identifier }
constant_value = { number | "public_input_start" }

// Lexical elements
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
number = @{ ASCII_DIGIT+ }